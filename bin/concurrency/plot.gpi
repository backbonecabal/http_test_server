# used by bin/run-concurrency-test

# noenhanced: underscores causing subscript in title
set terminal svg noenhanced size 3000,1000 dynamic
set output outfile

set multiplot layout 1,3 title sprintf("test: %s\n%s", test_name, parameters) font ",14"

set datafile separator ','

set xrange[0:test_time*1000]
set xtics rotate by -45 1000
set grid
unset key

#

set title "Request Lifetimes"
set xlabel "Offset from start (ms)"
set ylabel "Requests"
set yrange [0:]
set style arrow 1 heads back nofilled linecolor rgb "black"  linewidth 1 dashtype solid size screen  0.008,90.000,90.00
plot infile_lifetime using 2 : 1 : ($3-$2) : (0.0) with vector as 1

#

set title "Active Requests"
set xlabel "Offset from start (ms)"
set ylabel "Number of active requests"
plot infile_active using 1:2 with linespoints

#

set title "Request Rate"
set xlabel "Offset from start (ms)"
set ylabel "Requests / s"
plot infile_rate using 1:2 with linespoints
